plugins {
    id 'com.android.application'
    id 'kotlin-android'

    // Added plugin id
    id 'org.jetbrains.dokka'
    id 'kotlinx-serialization'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.searchSummarizer"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.6.10'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += "-Xuse-experimental=kotlin.RequiresOptIn"
}

configurations {
    ktlint
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.0-alpha04'
    implementation "androidx.activity:activity-compose:$activityComposeVersion"

    // Added dependencies
    ktlint 'com.pinterest:ktlint:0.44.0'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.17.0"
    implementation "com.google.accompanist:accompanist-insets:0.20.2"
    implementation "io.coil-kt:coil-compose:1.4.0"

    var koin_version = "3.1.5"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-navigation:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.navigation:navigation-compose:2.5.0-alpha03'
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.datastore:datastore:1.0.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    def ktor_client_version = "1.6.7"
    implementation "io.ktor:ktor-client-core:$ktor_client_version"
    implementation "io.ktor:ktor-client-android:$ktor_client_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_client_version"
    implementation "io.ktor:ktor-client-logging:$ktor_client_version"
    implementation "io.ktor:ktor-client-logging-jvm:$ktor_client_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("$buildDir/kdoc"))
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt", "android", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint/ktlint-result.xml"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt", "android", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint/ktlint-result.xml"
    ignoreExitValue true
}
